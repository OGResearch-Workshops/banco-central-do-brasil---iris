%% Simple optimal policy model file
%
% This is the model file (i.e. the description of the variables and
% equations) for a simple optimal policy exercise. The model has a simple
% aggregate demand equation, a Phillips curve, and two verions of monetary
% policy specification: (1) a simple rule, and (2) a loss function used to
% calculate optimal policy. Choose between the two specifications using the
% switch |optimal_policy|. Furthermore, given the loss function, the type
% of optimal policy calculated in IRIS can be either optimal discretionary
% policy, or optimal commitment policy. Use the option |Optimal=| at the
% time of loading the model file (i.e. in the function |model( )|) to
% choose one or the other.
%

%% Declare transition (aka endogenous) variables

!transition-variables

    "Output Gap" y, "Inflation" pi, "Policy Rate" r
    "Expected discounted policy loss" loss
    "Period policy loss" period_loss

%

%% Declare parameters
%
% Note that parameters are not assigned any values in the model file.
% Instead, they are assigned after the model file has been loaded and
% converted into a model object, in |read_model|.
%

!parameters

    del1, del2, sgm, alp, gam
    bet, lmb1, lmb2, lmb3, targ, cutoff
    mu, rho

%

%% Declare shocks

!transition-shocks

    "Demand shock" e, "Cost-push shock" u

%

%% Write transition (aka endogenous) equations
%
% Use the |min( )| operator to write a loss function; the term in the
% brackets (here |beta|) is the discount factor. The resulting optimal
% policy can be either discretionary or commitment, depending on the option
% |Optimal=| set in the funtion |model| when loading the model file.
%

!substitutions

    period_loss_expression := (pi-targ)^2 + lmb1*y^2 + lmb2*(r-r{-1})^2 + lmb3*max(pi-targ-cutoff,0)^2;


!transition-equations

    "Aggregate demand"
    y = del1*y{-1} + del2*y{+1} - sgm*(r - pi{1}) + e;

    "Phillips curve"
    pi = alp*pi{-1} + (1-alp)*pi{1} + gam*y + u;

    "Monetary policy"
    !if optimal_policy
        min(bet) $period_loss_expression$;
    !else
        r = rho*r{-1} + (1-rho)*(targ + mu*(pi{1} - targ));
    !end

    "Expected discounted policy loss"
    loss = $period_loss_expression$ + bet*loss{+1} !! loss = 0;

    "Period policy loss"
    period_loss = $period_loss_expression$;

%

